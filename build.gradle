import static org.gradle.internal.os.OperatingSystem.*

plugins {
    id 'maven'
    id 'java'
    id 'java-library'
    id "org.jetbrains.kotlin.jvm" version "1.3.50"
//    id "org.jetbrains.kotlin.kapt" version "1.3.10"
    id "com.github.johnrengelman.shadow" version '5.1.0'
}

// jitpack
group = 'com.github.kotlin_graphics'

ext {
    moduleName = "${group}.vkk"
    kotlin = 'org.jetbrains.kotlin:kotlin'
    kotlin_version = '1.3.50'
    uno_version = '3275c3ce3045be19c84288609d9f1631d631a743'
    gli_version = '336a02f5acd5db7f5c34cb673512714f1856577e'
    glm_version = '3d3aea6d420bace96ced057cddeca023129d7c41'
    unsigned_version = 'c81c2e39fb9e10403650c1b371b8573fc745bfb4'
    kool_version = '1e564cfb7ab72523d0f748a235e21cf33d0c73bc'
    lwjgl_version = "3.2.3"
    lwjgl_natives = current() == WINDOWS ? "windows" : current() == LINUX ? "linux" : "macos"
    sprivCross_version = "0.5.0-1.1.85"
}

dependencies {

    implementation "$kotlin-stdlib"
    implementation "$kotlin-reflect"

    def kx = "com.github.kotlin-graphics"
//    implementation "$kx:gln:$gln_version"
    implementation "$kx:gli:$gli_version"
    implementation "${kx}.glm:glm:$glm_version"
    implementation "$kx:kool:$kool_version"
    implementation "$kx:kotlin-unsigned:$unsigned_version"

    ["", "-glfw", "-jemalloc", "-openal", "-opengl", "-stb", "-vulkan"].each {
        implementation "org.lwjgl:lwjgl$it:$lwjgl_version"
        if (it != "-vulkan")
            runtime "org.lwjgl:lwjgl$it:$lwjgl_version:natives-$lwjgl_natives"
    }

    ext.spirvCross = "graphics.scenery:spirvcrossj:$sprivCross_version"
    implementation spirvCross
    runtime "$spirvCross:natives-$lwjgl_natives"

    // https://mvnrepository.com/artifact/com.google.code.findbugs/jsr305
    implementation "com.google.code.findbugs:jsr305:3.0.2"

//    ["core", "gl", "vk"].each {
//        testImplementation "${kx}.uno-sdk:uno-$it:$uno_version"
//    }

    testImplementation 'io.github.microutils:kotlin-logging:1.7.6'
//    compile group: 'org.jetbrains.kotlin.kapt', name: 'org.jetbrains.kotlin.kapt.gradle.plugin', version: '1.3.0-rc-146'
}

repositories {
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://jitpack.io' }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
//    archives javadocJar
}

jar {
    inputs.property("moduleName", moduleName)
//    manifest.attributes('Automatic-Module-Name': moduleName)
}

compileKotlin {
    // Enable Kotlin compilation to Java 8 class files with method parameter name metadata
    kotlinOptions {
        freeCompilerArgs = ["-XXLanguage:+InlineClasses", "-Xjvm-default=enable"]
        jvmTarget = "11"
//        javaParameters = true
    }
    // As per https://stackoverflow.com/a/47669720
    // See also https://discuss.kotlinlang.org/t/kotlin-support-for-java-9-module-system/2499/9
    destinationDir = compileJava.destinationDir
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
//        javaParameters = true
    }
}

compileJava {
    dependsOn(':compileKotlin')
    doFirst {
        options.compilerArgs = ['--module-path', classpath.asPath,]
        classpath = files()
    }
}

shadowJar.archiveClassifier = 'all'